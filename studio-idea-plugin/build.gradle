buildscript {
    repositories {
        maven {
            url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
        }
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.5.0"
}

apply from: "$rootDir/$project.name/verifier.gradle"

dependencies {
    compile("com.evolveum.midpoint.model:model-common:$midpointVersion") {
        transitive = false
    }
    compile("com.evolveum.midpoint.model:model-api:$midpointVersion") {
        transitive = false
    }
    compile("com.evolveum.midpoint.repo:security-api:$midpointVersion") {
        transitive = false
    }
    compile("com.evolveum.midpoint.model:notifications-api:$midpointVersion") {
        transitive = false
    }
    compile "com.evolveum.midpoint:midpoint-localization:$midpointVersion"
    compile("com.evolveum.midpoint:midscribe-core:$midscribeVersion") {
        exclude group: "org.springframework"
        exclude group: "net.sf.jasperreports"
        exclude group: "org.apache.cxf", module: "cxf-rt-wsdl"

        exclude group: "org.slf4j"
        exclude group: "ch.qos.logback"
    }

    compile "de.slackspace:openkeepass:$openkeepass"
    compile "org.apache.commons:commons-lang3:$commonsLang"
//    compile "org.knowm.xchart:xchart:$xchartVersion"
    compile "org.slf4j:slf4j-log4j12:$slf4jVersion"
    compile "com.squareup.okhttp3:okhttp:$okHttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // dependencies form midpoint-client, todo cleanup

    compile("com.evolveum.midpoint.model:model-impl:$midpointVersion") {
        transitive = false
    }
    compile("com.evolveum.midpoint.infra:common:$midpointVersion") {
        exclude group: "org.springframework"
        exclude group: "net.sf.jasperreports"
        exclude group: "org.apache.cxf"
        exclude group: "org.slf4j"
        exclude group: "ch.qos.logback"
    }

    compile "stax:stax:$staxVersion"

//    compile("com.evolveum.midpoint.infra:schema:4.2-SNAPSHOT") {
//        exclude group: "org.springframework"
//        exclude group: "net.sf.jasperreports"
//        exclude group: "org.apache.cxf"
//    }

    runtime "org.glassfish.jaxb:jaxb-runtime:2.3.2" // needed because of NamespacePrefixMapper class
    runtime("org.springframework:spring-core:5.0.7.RELEASE") {
        // needed because of DebugDumpable impl uses spring ReflectionUtils class
        transitive = false
    }
}

def channelSuffix = ""
if (!publishChannel?.isBlank() && publishChannel != "stable") {
    channelSuffix = "-" + publishChannel
}

def fullVersion = "$project.version.$buildNumber$channelSuffix"

println "Plugin version: " + fullVersion

intellij {
    version '2020.2'    // 'LATEST-EAP-SNAPSHOT'
    type 'IC'
    downloadSources true
    pluginName 'intellij-midpoint-studio'
    updateSinceUntilBuild false
    plugins = [
            'PsiViewer:202-SNAPSHOT.3',
            'com.jetbrains.hackathon.indices.viewer:1.8',
            'java',
            'Groovy',
            'maven'
    ]
    patchPluginXml {
        sinceBuild '193'
        untilBuild '202.*'
        version fullVersion

    }
    publishPlugin {
        channels publishChannel
        token System.getenv('studio_intellijPublishToken')
    }
}

runPluginVerifier {
    pluginFileName = "$intellij.pluginName-$version"
    ides = ["IC-2019.3", "IC-2020.1", "IC-2020.2", "IC-LATEST-EAP-SNAPSHOT"]
}
