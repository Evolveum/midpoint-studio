buildscript {
    repositories {
        maven {
            url 'https://dl.bintray.com/jetbrains/intellij-plugin-service'
        }
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.6.5"
}

dependencies {
    compile("com.evolveum.midpoint.model:model-common:$midpointVersion") {
        transitive = false
    }
    compile("com.evolveum.midpoint.model:model-api:$midpointVersion") {
        transitive = false
    }
    compile("com.evolveum.midpoint.model:model-impl:$midpointVersion") {
        transitive = false
    }
    compile("com.evolveum.midpoint.infra:common:$midpointVersion") {
        exclude group: "org.springframework"
        exclude group: "net.sf.jasperreports"
        exclude group: "org.apache.cxf"
        exclude group: "org.slf4j"
        exclude group: "ch.qos.logback"
    }
    compile("com.evolveum.midpoint.repo:security-api:$midpointVersion") {
        transitive = false
    }
    compile("com.evolveum.midpoint.model:notifications-api:$midpointVersion") {
        transitive = false
    }
    compile "com.evolveum.midpoint:midpoint-localization:$midpointVersion"

    compile("com.evolveum.midpoint:midscribe-core:$midscribeVersion") {
        exclude group: "org.springframework"
        exclude group: "net.sf.jasperreports"
        exclude group: "org.apache.cxf", module: "cxf-rt-wsdl"

        exclude group: "org.slf4j"
        exclude group: "ch.qos.logback"
    }

    compile "de.slackspace:openkeepass:$openkeepass"
    compile "org.apache.commons:commons-lang3:$commonsLang"
//    compile "org.knowm.xchart:xchart:$xchartVersion"
    compile "org.slf4j:slf4j-log4j12:$slf4jVersion"
    compile "com.squareup.okhttp3:okhttp:$okHttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    compile "stax:stax:$staxVersion"
    compile "xml-apis:xml-apis:$xmlApisVersion"

    runtime "org.glassfish.jaxb:jaxb-runtime:2.3.2" // needed because of NamespacePrefixMapper class
    runtime("org.springframework:spring-core:5.0.7.RELEASE") {
        // needed because of DebugDumpable impl uses spring ReflectionUtils class
        transitive = false
    }
}

if (gitLocalBranch == 'nightly' || gitLocalBranch == 'milestone') {
    publishChannel = gitLocalBranch
} else if (gitLocalBranch == 'stable') {
    publishChannel = 'default'
}

def channelSuffix = ""
if (!publishChannel?.isBlank()) {
    def channel = publishChannel.toLowerCase()
    if (channel == "nightly") {
        channelSuffix = "-" + buildNumber + "-" + channel
    } else if (channel == "milestone") {
        channelSuffix = "-" + buildNumber
    }
}

def fullVersion = "$project.version$channelSuffix"

println "Plugin version: " + fullVersion
println "Publish channel: " + publishChannel

intellij {
    version '2020.3'
    type 'IC'
    downloadSources true
    pluginName 'intellij-midpoint-studio'
    updateSinceUntilBuild false
    plugins = [
            'PsiViewer:203-SNAPSHOT',
            'com.jetbrains.hackathon.indices.viewer:1.11',
            'java',
            'Groovy',
            'maven'
    ]
    patchPluginXml {
        sinceBuild '202'
        untilBuild '211.*'
        version fullVersion
    }
    runPluginVerifier {
        // available versions
        // simple https://data.services.jetbrains.com/products?fields=code,name,releases.version,releases.build,releases.type&code=IIC
        // full https://data.services.jetbrains.com/products?fields=code,name,releases.downloads,releases.version,releases.build,releases.type&code=IIC
        ideVersions = ["IC-2020.2.4", "IC-2020.3.2", "IC-2021.1"]
    }
    publishPlugin {
        channels publishChannel
        token System.getenv('studio_intellijPublishToken')
    }
}
